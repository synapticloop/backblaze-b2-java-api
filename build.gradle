plugins {
	id 'java'
	id 'eclipse'
	id "maven"
	id "maven-publish"
	id 'net.saliman.cobertura' version '2.2.6'
	id "com.jfrog.bintray" version "1.4"
}

group = 'synapticloop'
archivesBaseName = 'backblaze-b2-java-api'
description = """An api for backblaze b2 storage in java"""

version = 'v1.0.0'

description = """backblaze-b2-java-api"""

sourceCompatibility = 1.7
targetCompatibility = 1.7



repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	runtime 'org.apache.httpcomponents:httpclient:4.3.4'
	runtime 'commons-io:commons-io:2.4'
	runtime 'org.json:json:20090211'
	runtime 'org.slf4j:slf4j-api:1.7.13'

	compile 'org.apache.httpcomponents:httpclient:4.3.4'
	compile 'commons-io:commons-io:2.4'
	compile 'org.json:json:20090211'
	compile 'org.slf4j:slf4j-api:1.7.13'

	testCompile 'junit:junit:4.7'
	testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.4.1'
	testCompile 'org.apache.logging.log4j:log4j-core:2.4.1'

	testRuntime 'org.apache.logging.log4j:log4j-slf4j-impl:2.4.1'
	testRuntime 'org.apache.logging.log4j:log4j-core:2.4.1'
	
}

/*
 * COBERTURA
 */

cobertura {
	coverageFormats = [ 'html', 'xml']
}

test {
	include '**/*Test.class' 
	maxParallelForks = 1
	forkEvery = 50 
}


/*
 *
 * Publish to bintray
 *
 */


def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'

tasks.withType(Javadoc) {
	options.links(javaApiUrl, groovyApiUrl)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

publishing {
	publications {
		Synapticloop(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar

			groupId 'synapticloop'
			artifactId 'backblaze-b2-java-api'

			pom.withXml {
				configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
					asNode().dependencies[0].dependency.find {
						it.artifactId[0].text() == dep.moduleName &&
								it.groupId[0].text() == dep.moduleGroup
					}.scope[0].value = 'compile'
				}
			}
		}
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_PASSWORD')

	publications = [ 'Synapticloop' ]

	publish = true

	pkg {
		repo = 'maven'
		name = 'backblaze-b2-java-api'
	}

}
